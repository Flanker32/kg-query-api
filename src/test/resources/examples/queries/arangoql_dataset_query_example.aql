FOR doc IN `minds-core-dataset-v0_0_4`
    LET license_info = (FOR license_info_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-license_info`
        RETURN {
            "url": license_info_doc.`http://schema.org/url`,
            "postFix": license_info_doc.`http://schema.org/postFix`
        }
    )
    LET contributors = (FOR contributors_doc, e IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-contributors` SORT e.orderNumber ASC
        RETURN {
            "postFix": contributors_doc.`http://hbp.eu/minds#shortName`,
            "uuid": contributors_doc.`https://nexus-dev.humanbrainproject.org/vocabs/nexus/core/terms/v0.1.0/uuid`,
            "identifier": contributors_doc.`http://schema.org/identifier`
        }
    )
    LET component = (FOR component_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-component`
        RETURN component_doc.`http://schema.org/postFix`
    )
    LET parcellation_atlas = (FOR parcellation_atlas_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-parcellationAtlas`
        RETURN parcellation_atlas_doc.`http://schema.org/postFix`)
    LET parcellation_region = (FOR parcellation_region_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-parcellationRegion`
        RETURN parcellation_region_doc.`http://schema.org/postFix`)
    LET publications = (FOR publications_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-publications`
        RETURN {
            "citation": publications_doc.`http://hbp.eu/minds#cite`,
            "doi": publications_doc.`http://hbp.eu/minds#doi`
        })

    LET doireference = (FOR doi_reference_doc IN 1..1 INBOUND doc `rel-hbp_eu-minds-doireference`
        RETURN {
            "citation": doi_reference_doc.`http://hbp.eu/minds#citation`,
            "doi": doi_reference_doc.`http://hbp.eu/minds#doi`
        })
    LET activity = (FOR activity_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-activity`
        LET preparation = (FOR preparation_doc IN 1..1 OUTBOUND activity_doc `rel-hbp_eu-minds-preparation`
            RETURN preparation_doc.`http://schema.org/postFix`
            )
        LET methods = (FOR methods_doc IN 1..1 OUTBOUND activity_doc `rel-hbp_eu-minds-methods`
            RETURN methods_doc.`http://schema.org/postFix`
            )
        LET protocols = (FOR protocols_doc IN 1..1 OUTBOUND activity_doc `rel-hbp_eu-minds-protocols`
            RETURN protocols_doc.`http://schema.org/postFix`
            )
        RETURN {
            "preparation": preparation,
            "methods": methods,
            "protocols": protocols
        })
    LET embargo = (FOR embargo_doc IN 1..1 OUTBOUND doc `rel-hbp_eu-minds-embargo_status`
        RETURN embargo_doc.`http://schema.org/postFix`
        )


    LET neuroglancer = (FOR neuroglancer_doc IN 1..1 INBOUND doc `rel-hbp_eu-minds-reference`
        RETURN {
            "postFix": neuroglancer_doc.`http://schema.org/postFix`,
            "url": neuroglancer_doc.`http://hbp.eu/minds#postfix`
            }
        )
    LET subjects = (FOR subjects_doc IN 2..2 OUTBOUND doc `rel-hbp_eu-minds-specimen_group`, `rel-hbp_eu-minds-subjects`

        LET sex = (FOR sex_doc IN 1..1 OUTBOUND subjects_doc `rel-hbp_eu-minds-sex`
            RETURN sex_doc.`http://schema.org/postFix`
        )
        //LET experimentmethod = (FOR experimentmethod_doc IN 1..1 OUTBOUND subjects_doc `rel-hbp_eu-minds-experimentmethod`
        //    RETURN experimentmethod_doc.`http://schema.org/postFix`
        //)
        LET samples = (FOR samples_doc IN 1..1 OUTBOUND subjects_doc `rel-hbp_eu-minds-samples`
            LET methods = (FOR methods_doc IN 1..1 OUTBOUND samples_doc `rel-hbp_eu-minds-methods`
                RETURN methods_doc.`http://schema.org/postFix`
            )
            LET files = (FOR files_doc IN 2..2 INBOUND doc `rel-hbp_eu-minds-associated_entity`, OUTBOUND `rel-hbp_eu-minds-file`
                RETURN {
                    "postFix": files_doc.`http://schema.org/postFix`,
                    "absolute_path": files_doc.`http://hbp.eu/minds#absolute_path`
                }
            )
            RETURN {
                "postFix": samples_doc.`http://schema.org/postFix`,
                "uuid": samples_doc.`https://nexus-dev.humanbrainproject.org/vocabs/nexus/core/terms/v0.1.0/uuid`,
                "identifier": samples_doc.`http://schema.org/identifier`,
                "methods": methods,
                "files": files
            }
        )


        LET species = (FOR species_doc IN 1..1 OUTBOUND subjects_doc `rel-hbp_eu-minds-species`
            RETURN {
                "postFix": species_doc.`http://schema.org/postFix`,
                "uuid": species_doc.`https://nexus-dev.humanbrainproject.org/vocabs/nexus/core/terms/v0.1.0/uuid`,
                "identifier": species_doc.`http://schema.org/identifier`
            }
        )

        RETURN {
            "postFix": subjects_doc.`http://schema.org/postFix`,
            "sex": sex,
            "strain": subjects_doc.`http://hbp.eu/minds#strains`,
            "genotype": subjects_doc.`http://hbp.eu/minds#genotype`,
          //  "experimentmethod": experimentmethod,
            "samples": samples,
            "species": species
        }
    )


    LET files = (FOR files_doc IN 2..2 INBOUND doc `rel-hbp_eu-minds-associated_entity`, OUTBOUND `rel-hbp_eu-minds-file`
            RETURN {
                "postFix": files_doc.`http://schema.org/postFix`,
                "absolute_path": files_doc.`http://hbp.eu/minds#absolute_path`
            }
        )

    RETURN {
        "license_info": license_info,
        "identifier": doc.`http://schema.org/identifier`,
        "title": doc.`http://schema.org/postFix`,
        "contributors": contributors,
        "component": component,
        "doireference": doireference,
        "parcellationAtlas": parcellation_atlas,
        "parcellationRegion": parcellation_region,
        "publications": publications,
        "activity": activity,
        "embargo": embargo,
        "description": doc.`http://schema.org/description`,
        "subjects": subjects,
        "external_datalink": doc.`http://hbp.eu/minds#external_datalink`,
        "releasedate": doc.`http://hbp.eu/minds#release_date`,
        "neuroglancer": neuroglancer,
        "files": files
    }